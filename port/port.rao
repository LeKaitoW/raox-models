
enum Состояние_буксира {Стоит_у_очереди, Стоит_у_причала, Занят}
enum Направление_буксира {К_очереди, К_причалам}
enum Номер_причала {_1, _2, _3} // костыыыль
enum Состояние_причала {Свободен, Занят}
enum Состояние_шторма {Спокоен, Штормит, Начался}
enum Тип_танкера {Тип1, Тип2, Тип3}
enum Состояние_танкера {Пришел, Ожидает_заполнения, Заполняется, Заполнен}

constant ширина = 350
constant высота = 600
constant высота_границы_очереди = высота / 3
constant высота_границы_причалов = 2 * высота / 3

type Порт {
	int количество_в_очереди;
	int номер_крайнего_танкера; 
}

type Буксиры {
	Состояние_буксира состояние_буксира;
	int количество_обслуженных;
	int номер_танкера;
	/* Анимация */
	int размер;
	int X;
	int Y;
	RaoColor цвет;
	Направление_буксира направление_буксира;
}

type Танкеры {
	Тип_танкера тип;
	Состояние_танкера состояние;
	double время_прихода;
	int порядковый_номер;
}

type Причалы {
	Номер_причала номер;
	Состояние_причала состояние_причала;
	int количество_обслуженных_причалом;
	Тип_танкера тип_танкера;
}

type Штормы {
	Состояние_шторма состояние;
	RaoColor цвет
}


resource порт = Порт.create(0, 0)
resource буксир = Буксиры.create(Состояние_буксира.Стоит_у_очереди, 0, 0, 30, ширина / 2, высота_границы_очереди, RaoColor.GREEN, Направление_буксира.К_причалам);
resource шторм = Штормы.create(Состояние_шторма.Спокоен, RaoColor.GRAY);


sequence интервал_прихода1 = new Values(#[10]);
sequence интервал_прихода2 = new Values(#[15]);
sequence интервал_прихода3 = new Values(#[20]);
sequence длительность_буксирования = new Values(#[5]);
sequence длительность_заполнения1 = new Values(#[31]);
sequence длительность_заполнения2 = new Values(#[32]);
sequence длительность_заполнения3 = new Values(#[33]);
sequence периодичность_шторма = new Values(#[90]);
sequence длительность_шторма = new Values(#[20]);

event Событие_прихода_танкера1() {
	Танкеры.create(Тип_танкера.Тип1, Состояние_танкера.Пришел, currentTime, порт.номер_крайнего_танкера + 1);
	Событие_прихода_танкера1.plan(currentTime + интервал_прихода1.next());
	порт.количество_в_очереди = порт.количество_в_очереди + 1;
	порт.номер_крайнего_танкера = порт.номер_крайнего_танкера + 1;
}

event Событие_прихода_танкера2() {
	Танкеры.create(Тип_танкера.Тип2, Состояние_танкера.Пришел, currentTime, порт.номер_крайнего_танкера + 1);
	Событие_прихода_танкера2.plan(currentTime + интервал_прихода2.next());
	порт.количество_в_очереди = порт.количество_в_очереди + 1;
	порт.номер_крайнего_танкера = порт.номер_крайнего_танкера + 1;
}

event Событие_прихода_танкера3() {
	Танкеры.create(Тип_танкера.Тип3, Состояние_танкера.Пришел, currentTime, порт.номер_крайнего_танкера + 1);
	Событие_прихода_танкера3.plan(currentTime + интервал_прихода3.next());
	порт.количество_в_очереди = порт.количество_в_очереди + 1;
	порт.номер_крайнего_танкера = порт.номер_крайнего_танкера + 1;
}

event Событие_начала_шторма() {
	шторм.состояние = Состояние_шторма.Начался;
	Событие_начала_шторма.plan(currentTime + периодичность_шторма.next());
}


operation Образец_шторма() {
	relevant _шторм = шторм.onlyif[состояние == Состояние_шторма.Начался];

	def duration() {
		return длительность_шторма.next();
	}
	def begin() {
		_шторм.состояние = Состояние_шторма.Штормит;
	}
	def end() {
		_шторм.состояние = Состояние_шторма.Спокоен;		
	}
}

operation Образец_буксирования_пустого_танкера() {
	relevant _буксир = буксир.onlyif[
		состояние_буксира == Состояние_буксира.Стоит_у_очереди && порт.количество_в_очереди > 0
		];
	relevant _шторм = шторм.onlyif[состояние == Состояние_шторма.Спокоен];
	relevant _Танкер = Танкеры.all.filter[состояние == Состояние_танкера.Пришел].minBySafe[время_прихода];	// стоит добавить условие наличия своб. причалов

	def duration() {
		return длительность_буксирования.next();
	}
	def begin() {
		порт.количество_в_очереди = порт.количество_в_очереди - 1;
		_буксир.состояние_буксира = Состояние_буксира.Занят;
		_буксир.направление_буксира = Направление_буксира.К_причалам;
		_буксир.номер_танкера = _Танкер.порядковый_номер;
		
		/* Анимация */
		_буксир._Y = высота_границы_очереди + _буксир.размер / 2; // на полкорпуса ниже границы очереди
	}
	def end() {
		_буксир.состояние_буксира = Состояние_буксира.Стоит_у_причала;
		_Танкер.состояние = Состояние_танкера.Ожидает_заполнения;
		
		/* Анимация */
		_буксир._Y = высота_границы_причалов - 3 * _буксир.размер / 2;
	}
}


operation Образец_заполнения_танкера() {
	relevant _буксир = буксир.onlyif[состояние_буксира == Состояние_буксира.Стоит_у_причала]; 					// ?зачем?
	relevant _Танкер = Танкеры.accessible.filter[состояние == Состояние_танкера.Ожидает_заполнения].any; 
	relevant _Причал = Причалы.accessible.filter[состояние_причала == Состояние_причала.Свободен].any;	
	
	def duration(){
		switch _Танкер.тип {
		case Тип_танкера.Тип1:
			return длительность_заполнения1.next()
		case Тип_танкера.Тип2:
			return длительность_заполнения2.next()
		case Тип_танкера.Тип3:
			return длительность_заполнения3.next()
		}
	}

	def begin() {
		_Причал.состояние_причала = Состояние_причала.Занят;
		_Танкер.состояние = Состояние_танкера.Заполняется;
		_Причал.тип_танкера = _Танкер.тип;
	}
	def end() {
		_Причал.состояние_причала = Состояние_причала.Свободен;
		_Причал.количество_обслуженных_причалом = _Причал.количество_обслуженных_причалом + 1;
		_Танкер.состояние = Состояние_танкера.Заполнен;
	}
}


operation Образец_буксирования_заполненного_танкера() {
	relevant _буксир = буксир.onlyif[состояние_буксира == Состояние_буксира.Стоит_у_причала];
	relevant _шторм = шторм.onlyif[состояние == Состояние_шторма.Спокоен];
	relevant _Танкер = Танкеры.all.filter[состояние == Состояние_танкера.Заполнен].any;		

	def duration() {
		return длительность_буксирования.next();
	}
	def begin() {
		_буксир.состояние_буксира = Состояние_буксира.Занят;
		_буксир.направление_буксира = Направление_буксира.К_очереди;
		_буксир.номер_танкера = _Танкер.порядковый_номер;
		
		/* Анимация */
		_буксир._Y = высота_границы_причалов - 3 * _буксир.размер / 2; 
	}
	def end() {
		_буксир.состояние_буксира = Состояние_буксира.Стоит_у_очереди;
		_буксир.количество_обслуженных =  _буксир.количество_обслуженных + 1;
		
		/* Анимация */
		_буксир._Y = высота_границы_очереди + _буксир.размер / 2;
	}
}


operation Образец_перемещения_пустого_буксира_к_очереди() {
	relevant _буксир = буксир.onlyif[
		состояние_буксира == Состояние_буксира.Стоит_у_причала && порт.количество_в_очереди > 0
		];
	relevant _шторм = шторм.onlyif[состояние == Состояние_шторма.Спокоен];
	relevant _Танкер = Танкеры.all.filter[состояние == Состояние_танкера.Пришел].any;
	relevant _Причал = Причалы.all.filter[состояние_причала == Состояние_причала.Свободен].any;	
	
	def duration() {
		return длительность_буксирования.next();
	}
	def begin() {
		_буксир.состояние_буксира = Состояние_буксира.Занят;
		_буксир.направление_буксира = Направление_буксира.К_очереди;
		_буксир.номер_танкера = 0;
		
		/* Анимация */
		_буксир._Y = высота_границы_причалов - 3 * _буксир.размер / 2;
	}
	def end() {
		_буксир.состояние_буксира = Состояние_буксира.Стоит_у_очереди;
		
		/* Анимация */
		_буксир._Y = высота_границы_очереди + _буксир.размер / 2;
	}
}


logic Работа_модели {
	activity буксирование_танкера_к_причалу = new Activity(Образец_буксирования_пустого_танкера.create());
	activity заполнение_танкера = new Activity(Образец_заполнения_танкера.create());
	activity буксирование_танкера_от_причала = new Activity(Образец_буксирования_заполненного_танкера.create());
	activity штормление = new Activity(Образец_шторма.create());
	activity возвращение_буксира_к_очереди = new Activity(Образец_перемещения_пустого_буксира_к_очереди.create());
}
boolean Триггер(Буксиры буксир){
	return false;
}

def init() {
	Timer.plan(currentTime + 1);
	Причалы.create(Номер_причала._1, Состояние_причала.Свободен, 0, null);
	Причалы.create(Номер_причала._2, Состояние_причала.Свободен, 0, null);
	Причалы.create(Номер_причала._3, Состояние_причала.Свободен, 0, null);
	Событие_прихода_танкера1.plan(currentTime + интервал_прихода1.next());
	Событие_прихода_танкера2.plan(currentTime + интервал_прихода2.next());
	Событие_прихода_танкера3.plan(currentTime + интервал_прихода3.next());
	Событие_начала_шторма.plan(currentTime + периодичность_шторма.next());       
}

def terminateCondition() {
	return currentTime >= 240; //7 * 12 * 60;
}


result занятость_буксира = new Result([буксир.состояние_буксира]);
result длина_очереди = new Result([порт.количество_в_очереди]);
result Всего_обслужено_Митей = new Result([буксир.количество_обслуженных]);
result занятость_причала_1 = new Result([Причалы.all.filter[номер == Номер_причала._1].any.состояние_причала]);
result занятость_причала_2 = new Result([Причалы.all.filter[номер == Номер_причала._2].any.состояние_причала]);
result занятость_причала_3 = new Result([Причалы.all.filter[номер == Номер_причала._3].any.состояние_причала]);
result Всего_заполнено_причалом_1 = new Result([Причалы.all.filter[номер == Номер_причала._1].any.количество_обслуженных_причалом]);
result Всего_заполнено_причалом_2 = new Result([Причалы.all.filter[номер == Номер_причала._2].any.количество_обслуженных_причалом]);
result Всего_заполнено_причалом_3 = new Result([Причалы.all.filter[номер == Номер_причала._3].any.количество_обслуженных_причалом]);
result Пропускная_способность = new Result([буксир.количество_обслуженных / currentTime * 60]);
result Длительность_работы = new Result([currentTime / 60]);

/* Animation */



event Timer() {
	//relevant _буксир = буксир;
	
	if (буксир.состояние_буксира == Состояние_буксира.Занят) {
		val шаг_хода_буксира = (((высота_границы_причалов - 1.5 * буксир.размер) 
								- (высота_границы_очереди + 1.5 * буксир.размер)) / 5) as int // исправить 5 на int(длительность_буксирования)
		if (буксир.направление_буксира == Направление_буксира.К_причалам){
			буксир._Y = буксир._Y + шаг_хода_буксира
			//log("Буксир едет к причалу. Y = " + буксир._Y + ". T: " + currentTime)
		}
		if (буксир.направление_буксира == Направление_буксира.К_очереди){
			буксир._Y = буксир._Y - шаг_хода_буксира
			//log("Буксир едет к очереди. Y = " + буксир._Y + ". T: " + currentTime)
		}
	}
		
	Timer.plan(currentTime + 1);
}

RaoColor Цвет_по_типу (Тип_танкера тип){
	switch тип {
		case Тип_танкера.Тип1:
			return RaoColor.RED
		case Тип_танкера.Тип2:
			return RaoColor.BLUE
		case Тип_танкера.Тип3:
			return RaoColor.YELLOW
		default:
			return RaoColor.BLACK
	}
		
}

frame Кадр_порта {
	def init() {
		background = new Background(ширина, высота, RaoColor.WHITE)
	}	
	
	def draw() {
		/* Фон */
		if (шторм.состояние != Состояние_шторма.Спокоен){
			drawImage("images/storm.png", 0, 0);
		}
		else{
			drawImage("images/calm.png", 0, 0);
		}
		
			
		/* Отрисовка статики */	
		drawLine(0, высота_границы_очереди, ширина, высота_границы_очереди, RaoColor.BLACK); // Зона очереди
		drawLine(0, высота_границы_причалов, ширина, высота_границы_причалов, RaoColor.BLACK); // Зона причалов
			
		/* Отрисовка */
		drawRectangle(буксир._X, буксир._Y, буксир.размер, буксир.размер, буксир.цвет, буксир.цвет);
		drawText(Integer.toString(буксир._количество_обслуженных), буксир._X + 3, буксир._Y + 3); // изменить число
		
		var вектор = 0;
		for (Танкеры плывущий_танкер : Танкеры.all.filter[порядковый_номер == буксир.номер_танкера]){
			if (буксир.направление_буксира == Направление_буксира.К_причалам)
				вектор = -2
			else
				вектор = 2
		drawRectangle(буксир._X, буксир._Y + вектор * буксир.размер, 30, 30, Цвет_по_типу(плывущий_танкер.тип), RaoColor.BLACK)
		}
		
		
		
		var i = 0;
		for (Танкеры танк : Танкеры.all.filter[состояние == Состояние_танкера.Пришел]){
			drawRectangle(15 + (i % 8) * 40, высота_границы_очереди - 35 - (i / 8) * 40, 30, 30, Цвет_по_типу(танк.тип), RaoColor.BLACK)
			i++;
		}
		
		for (Причалы _причал : Причалы.all){
			var отступ = 0;
			switch _причал.номер {
				case _1:
					отступ = 0
				case _2:
					отступ = 2
				case _3:
					отступ = 4
			}
			
			drawRectangle(50 + отступ * 50, высота - 20, 40, 20, Цвет_по_типу(_причал.тип_танкера), RaoColor.BLACK)
			
		}
		//Танкеры танкер_причала_1 = Танкеры.all.filter[состояние == Состояние_танкера.Заполняется]
		
	}
}


