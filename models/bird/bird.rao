constant bg_forest_b_width = 1280;
constant bg_forest_b_height = 381;
constant bg_forest_c_height = 360;
constant width = bg_forest_b_width;
constant height = bg_forest_b_height;

type Sprite {
	int bg_forest_b_left;
	int bg_forest_c_left;
	int bird_y;
	String bird_sprite_image;
}

sequence birdSpriteName = new Values(
	#["img/bird/frame-1.png", "img/bird/frame-2.png", "img/bird/frame-3.png", "img/bird/frame-4.png",
		"img/bird/frame-5.png", "img/bird/frame-6.png", "img/bird/frame-7.png", "img/bird/frame-8.png"])
sequence bird_dx = new Uniform(123456789, -1, 1);

resource sprite = Sprite.create(0, 0, 20, birdSpriteName.next());

event Timer() {
	if (sprite.bg_forest_b_left <= 0)
		sprite.bg_forest_b_left = width
	else
		sprite.bg_forest_b_left = sprite.bg_forest_b_left - 1

	if (sprite.bg_forest_c_left <= 0)
		sprite.bg_forest_c_left = width
	else
		sprite.bg_forest_c_left = sprite.bg_forest_c_left - 2

	val bird_dx_current = bird_dx.next();
	if (bird_dx_current > 0.3)
		sprite.bird_y = sprite.bird_y + 1
	else if (bird_dx_current < -0.3)
		sprite.bird_y = sprite.bird_y - 1

	if (sprite.bird_y < 0)
		sprite.bird_y = 0
	if (sprite.bird_y > 100)
		sprite.bird_y = 100
	if (currentTime % 3 == 0)
		sprite.bird_sprite_image = birdSpriteName.next();

	Timer.plan(currentTime + 1);
}

def init() {
	Timer.plan(currentTime + 1);
}

def terminateCondition() {
	return currentTime > 1000000;
}

frame WhosThat {
	def init() {
		background = new Background(width, height, new RaoColor(206, 230, 207));
	}

	def draw() {
		drawImage("img/background/bg_forest_b.png", 0, 0, width - sprite.bg_forest_b_left, bg_forest_b_height,
			sprite.bg_forest_b_left, 0, width - sprite.bg_forest_b_left, bg_forest_b_height);
		drawImage("img/background/bg_forest_b.png", width - sprite.bg_forest_b_left, 0, sprite.bg_forest_b_left,
			bg_forest_b_height, 0, 0, sprite.bg_forest_b_left + 1, bg_forest_b_height);

		drawImage(sprite.bird_sprite_image, 100, sprite.bird_y, 50, 34);

		drawImage("img/background/bg_forest_c.png", 0, 0, width - sprite.bg_forest_c_left, bg_forest_c_height,
			sprite.bg_forest_c_left, height - bg_forest_c_height, width - sprite.bg_forest_c_left, bg_forest_c_height);
		drawImage("img/background/bg_forest_c.png", width - sprite.bg_forest_c_left, 0, sprite.bg_forest_c_left,
			bg_forest_c_height, 0, height - bg_forest_c_height, sprite.bg_forest_c_left + 1, bg_forest_c_height);
	}
}

